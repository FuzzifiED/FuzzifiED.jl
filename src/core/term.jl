"""
    mutable struct Term

A `Term` object records a term that looks like ``Uc^{(p_1)}_{o_1}c^{(p_2)}_{o_2}\\dots c^{(p_l)}_{o_l}`` in an operator

# Fields
- `coeff :: ComplexF64` records the coefficient ``U``
- `cstr :: Vector{Int64}` is a length-``2l`` vector ``(p_1,o_1,p_2,o_2,\\dots p_l,o_l)`` recording the operator string

# Method 

It can be generated by the function

    function Term(coeff :: ComplexF64, cstr :: Vector{Int64}) :: ComplexF64 
"""
mutable struct Term 
    coeff :: ComplexF64 
    cstr :: Vector{Int64}
end 


"""
    function *(fac :: Number, tms :: Vector{Term}) :: Vector{Term}
    function -(tms :: Vector{Term}) :: Vector{Term}
    function *(tms :: Vector{Term}, fac :: Number) :: Vector{Term}
    function /(tms :: Vector{Term}, fac :: Number) :: Vector{Term}

Return the product of a collection of terms with a number. 
"""
function *(fac :: Number, tms :: Vector{Term})
    return [ Term(fac * tm.coeff, tm.cstr) for tm in tms ]
end
function -(tms :: Vector{Term})
    return (-1) * tms
end
function *(tms :: Vector{Term}, fac :: Number)
    return fac * tms
end
function /(tms :: Vector{Term}, fac :: Number)
    return (1 / fac) * tms
end

"""
    function +(tms1 :: Vector{Term}, tms2 :: Vector{Term}) :: Vector{Term}
    function -(tms1 :: Vector{Term}, tms2 :: Vector{Term}) :: Vector{Term}

Return the naive sum of two series of terms by taking their union. 
"""
function +(tms1 :: Vector{Term}, tms2 :: Vector{Term})
    return [ tms1 ; tms2 ]
end
function -(tms1 :: Vector{Term}, tms2 :: Vector{Term})
    return tms1 + (-tms2)
end

"""
    function *(tms1 :: Vector{Term}, tms2 :: Vector{Term}) :: Vector{Term}

Return the naive product of two series of terms. The number of terms equals the product of the number of terms in `tms1` and `tms2`. For each term in `tms1` ``Uc^{(p_1)}_{o_1}\\dots`` and `tms2` ``U'c^{(p'_1)}_{o'_1}\\dots``, a new term is formed by taking ``UU'c^{(p_1)}_{o_1}\\dots c^{(p'_1)}_{o'_1}\\dots``
"""
function *(tms1 :: Vector{Term}, tms2 :: Vector{Term})
    return vcat([ Term(tm1.coeff * tm2.coeff, [tm1.cstr ; tm2.cstr])
        for tm1 in tms1, tm2 in tms2 ]...)
end

function adjoint(tm :: Term)
    nc = length(tm.cstr)
    cstr1 = [ isodd(i) ? 1 - tm.cstr[nc - i] : tm.cstr[nc + 2 - i] for i = 1 : nc]
    return Term(conj(tm.coeff), cstr1)
end
"""
    function adjoint(tm :: Term) :: Term
    function adjoint(tms :: Vector{Term}) :: Vector{Term}

Return the Hermitian conjugate of a series of terms. For each term ``Uc^{(p_1)}_{o_1}c^{(p_2)}_{o_2}\\dots c^{(p_l)}_{o_l}``, the adjoint is ``\\bar{U}c^{(1-p_l)}_{o_l}\\dots c^{(1-p_2)}_{o_2}c^{(1-p_1)}_{o_1}``
"""
function adjoint(tms :: Vector{Term})
    return adjoint.(tms)
end
